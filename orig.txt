# This sentinel policies enforce encryption at rest utilizing customer managed kms keys
# These sentinel policies enforce Wells Fargo security principles PR-036,PR-037,PR-039

# Import common-functions/tfplan-functions/tfplan-functions.sentinel
# with alias "plan"
import "tfplan-functions" as plan
import "strings"
import "types"

# Get all Composer Instances
allComposerResources = plan.find_resources("google_composer_environment")

# Resource Types CMEK Param Map
resourceTypesCMEKKeyMap = {
	"google_pubsub_topic": {
		"key":   "kms_key_name",
		"array": null,
	},
	"google_secret_manager_secret": {
		"key":   "customer_managed_encryption.0.kms_key_name",
		"array": "replication.0.user_managed.0.replicas",
	},
	"google_dataproc_cluster": {
		"key":   "cluster_config.0.encryption_config.0.kms_key_name",
		"array": null,
	},
	"google_bigquery_dataset": {
		"key":   "default_encryption_configuration.0.kms_key_name",
		"array": null,
	},
	"google_storage_bucket": {
		"key":   "encryption.0.default_kms_key_name",
		"array": null,
	},
	"google_sql_database_instance": {
		"key":   "encryption_key_name",
		"array": null,
	},
    "google_dataflow_job": {
		"key":   "kms_key_name",
		"array": null,
	},
}

# Function to check if KMS Param is Defined
check_for_kms = func(rc, kms_key_param) {
	message = null
	address = rc["address"]
	unknown_kms_key = plan.evaluate_attribute(rc.change.after_unknown, kms_key_param)
	is_unknown_kms_undefined = rule { types.type_of(unknown_kms_key) is "null" }
	if is_unknown_kms_undefined {
		known_kms_key = plan.evaluate_attribute(rc, kms_key_param)
		is_known_kms_undefined = rule { types.type_of(known_kms_key) is "undefined" }
		if is_known_kms_undefined {
			message = plan.to_string(address) + " does not have " + kms_key_param + " defined"
		} else {
			if types.type_of(known_kms_key) is "null" {
				message = plan.to_string(address) + " does not have " + kms_key_param + " defined"
			}
		}
	}
	return message
}

check_multiple_kms = func(address, rc, kms_key_param, array) {
	kms_key_param_array = []
	# messages = {}
	if types.type_of(array) is "null" {
		append(kms_key_param_array, kms_key_param)
	} else {
		kms_attribs = plan.evaluate_attribute(rc, array)
		count = 0
		for kms_attribs as _ {
			append(kms_key_param_array, array + "." + plan.to_string(count) + "." + kms_key_param)
			count += 1
		}
	}
	err = []
	for kms_key_param_array as kkp {
		msg = check_for_kms(rc, kkp)
		if types.type_of(msg) is not "null" {
			append(err, msg)
		}
	}
	return err
}

# Fetch all resources based on resourceTypes
allResources = {}
for resourceTypesCMEKKeyMap as rt, _ {
	resources = plan.find_resources(rt)
	for resources as address, rc {
		allResources[address] = rc
	}
}

# print(messages)
msgs = {}
for allResources as address, rc {
	msg = check_multiple_kms(
		address,
		rc,
		resourceTypesCMEKKeyMap[rc["type"]]["key"],
		resourceTypesCMEKKeyMap[rc["type"]]["array"],
	)
	if length(msg) > 0 {
		msgs[address] = msg
	}
}

# Main rule
for msgs as k, v {
	print(k + " =====>" + plan.to_string(v))
}

GCP_RES_CMEK = rule { length(msgs) is 0 }

############################# GCP_COMPOSER_CMEK ##############################################
############################# Import tfplan-functions, strings ###############################
#Example: projects/sublime-lyceum-343813/locations/us-central1/keyRings/keyring-1/cryptoKeys/cryptokey-1



voilations_kmskeyname = {}

for allComposerResources as address, rc {
	kmskeyname_value = plan.evaluate_attribute(rc, "config.0.encryption_config.0.kms_key_name")

	iskmskeyname_undefined = rule { types.type_of(kmskeyname_value) is "undefined" or types.type_of(kmskeyname_value) is "null" }

	if iskmskeyname_undefined {
		voilations_kmskeyname[address] = rc
		print("The value of kms_key_name attribute in resource " + address + " can't be empty.")
	} else {
		kms_projects = strings.split(kmskeyname_value, "/")[0]
		kms_locations = strings.split(kmskeyname_value, "/")[2]
		kms_keyrings = strings.split(kmskeyname_value, "/")[4]
		kms_cryptokeys = strings.split(kmskeyname_value, "/")[6]

		if not (kms_projects is "projects" and kms_locations is "locations" and kms_keyrings is "keyRings" and kms_cryptokeys is "cryptoKeys") {
			voilations_kmskeyname[address] = rc
			print("The value of kms_key_name attribute in resource " + address + " is not according to standards.")
			print("The KMS CMEK URI should be in the format of projects/{projectId}/locations/{location}/keyRings/{keyRingName}/cryptoKeys/{cryptoKeyName}")
		}
	}
}

GCP_COMPOSER_CMEK = rule { length(voilations_kmskeyname) is 0 }

main = rule { GCP_RES_CMEK and GCP_COMPOSER_CMEK }